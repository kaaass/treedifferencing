plugins {
    id 'application'
}

dependencies {
    implementation project(':client')
    implementation project(':client.diff')
    implementation project(':gen.antlr3')
    implementation project(':gen.antlr3-antlr')
    implementation project(':gen.antlr3-json')
    implementation project(':gen.antlr3-php')
    implementation project(':gen.antlr3-r')
    implementation project(':gen.antlr3-xml')
    implementation project(':gen.c')
    implementation project(':gen.jdt')
    implementation project(':gen.js')
    implementation project(':gen.ruby')
}

jar.enabled = false
run.enabled = false

task computeResults(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.github.gumtree.benchmark.Defects4JLauncher'
    args "${project.projectDir}/defects4j", "${project.buildDir}/reports/defects4j.csv"
}

task plotResults(type: Exec) {
    commandLine "python3", "${project.projectDir}/src/main/python/plotDefects4J.py", "${project.buildDir}/reports/defects4j.csv"
}

task testSizeRegressions(type: Exec) {
    commandLine "python3", "${project.projectDir}/src/main/python/testRegressions.py", "${project.projectDir}/defects4j.csv", "${project.buildDir}/reports/defects4j.csv", "size"
}

task displaySizeDifferences(type: Exec) {
    commandLine "python3", "${project.projectDir}/src/main/python/displayDifferences.py", "${project.projectDir}/defects4j.csv", "${project.buildDir}/reports/defects4j.csv", "size"
}

task testRuntimeRegressions(type: Exec) {
    commandLine "python3", "${project.projectDir}/src/main/python/testRegressions.py", "${project.projectDir}/defects4j.csv", "${project.buildDir}/reports/defects4j.csv", "runtime"
}

task runBenchmark() {
    dependsOn computeResults, plotResults, testSizeRegressions, testRuntimeRegressions
}